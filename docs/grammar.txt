PROGRAM ::= (STATEMENT | FUNCTION | STRUCT | ANNOTATION_DEF | IMPORT)*

STATEMENT ::=
    | ASSIGNMENT
    | IF
    | WHILE
    | EXPRESSION ';'

EXPRESSION ::=
    | BINARY_EXPR
    | SCOPE

BINARY_EXPR ::= UNARY (BIN_OPERATOR UNARY)*
BIN_OPERATOR ::=
    | '==' | '!=' | '<' | '<=' | '>' | '>=' # Comparison
    | '&&' | '||'                           # Logical
    | '+' | '-' | '*' | '/' | '%'           # Arithmetic
    | '<<' | '>>' | '&' | '|' | '^'         # Bitwise

UNARY ::= UN_OPERATOR UNARY | PRIMARY_EXPRESSION
UN_OPERATOR ::= 
    | '+' | '-' | '!' # Arithmetic
    | '~'             # Bitwise
    | '*' | '&'       # Pointer

PRIMARY_EXPRESSION ::=
    | <number> | 'true' | 'false' | <char> | <string>
    | '[' (EXPRESSION (',' EXPRESSION)*)? ']' # Array Literal
    | <identifier> SUFFIX?
    | '(' EXPRESSION ')'

SUFFIX ::=
    | '(' (EXPRESSION (',' EXPRESSION)*)? ')' SUFFIX # Funcion Call
    | '[' EXPRESSION ']' SUFFIX                      # Array Access
    | '.' <identifier> SUFFIX                        # Member
    | ASSIGNMENT_SUFFIX
    | Îµ                                              # NONE

ASSIGNMENT ::= 'let' 'mut'? <identifier> TYPE_DEFINITION ASSIGNMENT_SUFFIX ';'
ASSIGNMENT_SUFFIX = '=' (SCOPE | EXPRESSION)

ANNOTATION ::= ANNOTATION_MULTI | ANNOTATION_USE
ANNOTATION_MULTI ::= '@optiz' ANNOTATION_REPEATED
ANNOTATION_USE   ::= '@use' <identifier>
CONTRACT ::= '@contract' ANNOTATION_REPEATED

ANNOTATION_REPEATED ::= '(' ANNOTATION_BASE (',' ANNOTATION_BASE)* ')'
ANNOTATION_BASE  ::= <identifier> ('=' ( EXPRESSION | '{' <tokens> '}' ))?              # <tokens> accepts all language tokens, but curly braces

SCOPE ::= ANNOTATION? CONTRACT? '{' (STATEMENT ';')* EXPRESSION? '}'

TYPE_DEFINITION ::= ':' TYPE
TYPE ::=
    | <identifier>
    | '[' TYPE (';' <number>)? ']'
    | '*' TYPE

IF ::= 'if' EXPRESSION SCOPE 'then' ('else' (IF | SCOPE))?

WHILE ::= 'while' EXPRESSION 'do' SCOPE

FUNCTION ::= 'fn' <identifier> '(' PARAMS? ')' TYPE_DEFINITION SCOPE
PARAMS ::= <identifier> TYPE_DEFINITION (',' <identifier> TYPE_DEFINITION)*

STRUCT ::= 'struct' <identifier> '{' (<identifier> TYPE_DEFINITION)* '}'

ANNOTATION_DEF ::= '@profile' ANNOTATION_REPEATED

IMPORT ::= 'import' <string>
